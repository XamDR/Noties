plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-parcelize'
	id 'dagger.hilt.android.plugin'
	id 'kotlin-kapt'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
	namespace 'net.azurewebsites.noties'
	compileSdk 31

	defaultConfig {
		applicationId "net.azurewebsites.noties"
		minSdk 23
		targetSdk 31
		versionCode 1
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		javaCompileOptions {
			annotationProcessorOptions {
				arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
			}
		}
	}
	buildTypes {
		debug {
			applicationIdSuffix ".debug"
			debuggable true
			versionNameSuffix ".debug"
			resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
		}
		release {
			debuggable false
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			resValue "string", "app_version", "${defaultConfig.versionName}"
		}
	}
	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}
	compileOptions {
		coreLibraryDesugaringEnabled true
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		dataBinding true
	}
	tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
		kotlinOptions.freeCompilerArgs += ["-Xallow-kotlin-package"]
	}
}

dependencies {
	implementation "androidx.activity:activity-ktx:1.5.0"
	implementation "androidx.core:core-ktx:1.8.0"
	implementation "androidx.core:core-splashscreen:1.0.0-rc01"
	implementation "androidx.appcompat:appcompat:1.4.2"
	implementation "androidx.asynclayoutinflater:asynclayoutinflater:1.0.0"
	implementation "androidx.constraintlayout:constraintlayout:2.1.4"
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.0"
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.0"
	implementation "androidx.navigation:navigation-fragment-ktx:2.5.0"
	implementation "androidx.navigation:navigation-ui-ktx:2.5.0"
	implementation "androidx.preference:preference-ktx:1.2.0"
	implementation "androidx.recyclerview:recyclerview:1.2.1"
	implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
	implementation "com.google.android.material:material:1.6.1"
	coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

	// Room
	implementation "androidx.room:room-runtime:$room_version"
	kapt "androidx.room:room-compiler:$room_version"
	implementation "androidx.room:room-ktx:$room_version"

	// Hilt
	implementation "com.google.dagger:hilt-android:2.41"
	kapt "com.google.dagger:hilt-compiler:2.41"

	// Exif Interface
	implementation "androidx.exifinterface:exifinterface:1.3.3"

	// Shimmer effect
	implementation "com.facebook.shimmer:shimmer:0.5.0"

	// Gif support
	implementation "pl.droidsonroids.gif:android-gif-drawable:1.2.24"

	// Jsoup
	implementation "org.jsoup:jsoup:1.15.1"

	// Testing
	testImplementation "junit:junit:4.13.2"
	androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
	androidTestImplementation "androidx.test.ext:junit:1.1.3"
	androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}