import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
	id 'org.jetbrains.kotlin.plugin.parcelize'
	id 'org.jetbrains.kotlin.kapt'
	id 'com.google.devtools.ksp'
	id 'com.google.dagger.hilt.android'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
	namespace 'io.github.xamdr.noties'
	compileSdk 34

	defaultConfig {
		applicationId "io.github.xamdr.noties"
		minSdk 23
		targetSdk 33
		versionCode 1
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		ksp {
			arg("room.schemaLocation", "$projectDir/schemas")
		}
	}
	buildTypes {
		debug {
			applicationIdSuffix ".debug"
			debuggable true
			versionNameSuffix ".debug"
			resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
		}
		release {
			debuggable false
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			resValue "string", "app_version", "${defaultConfig.versionName}"
		}
	}
	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}
	compileOptions {
		coreLibraryDesugaringEnabled true
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
	kotlin {
		jvmToolchain(17)
	}
	buildFeatures {
		viewBinding true
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion = "1.5.0"
	}
	tasks.withType(KotlinCompile).configureEach {
		kotlinOptions.freeCompilerArgs += ["-Xallow-kotlin-package"]
	}
}

dependencies {
	implementation libs.androidx.activity.ktx
	implementation libs.androidx.appcompat
	implementation libs.androidx.core.ktx
	implementation libs.androidx.core.splashscreen
	implementation libs.androidx.constraintLayout
	implementation libs.androidx.lifecycle.livedata.ktx
	implementation libs.androidx.lifecycle.runtime.ktx
	implementation libs.androidx.lifecycle.runtime.compose
	implementation libs.androidx.navigation.fragment.ktx
	implementation libs.androidx.preference.ktx
	implementation libs.androidx.recyclerview
	implementation libs.material
	coreLibraryDesugaring libs.android.desugarJdkLibs

	// Room
	implementation libs.room.runtime
	ksp libs.room.compiler
	implementation libs.room.ktx

	// Hilt
	implementation libs.hilt.android
	implementation libs.hilt.compose
	kapt libs.hilt.compiler

	// Exif Interface
	implementation libs.androidx.exif

	// Gif support
	implementation libs.gif

	// Timber
	implementation libs.timber

	// Media3
	implementation libs.media3
	implementation libs.media3.ui

	// Compose
	implementation libs.activity.compose
	implementation platform(libs.compose.bom)
	implementation libs.ui
	implementation libs.ui.graphics
	implementation libs.ui.tooling.preview
	implementation libs.material3
	implementation libs.material.icons.extended
	implementation libs.coil.compose
	implementation libs.androidx.ui.util

	// Jsoup
	implementation libs.jsoup

	// Testing
	testImplementation libs.junit4
	androidTestImplementation libs.androidx.core.testing
	androidTestImplementation libs.androidx.junit.testing
	androidTestImplementation libs.androidx.espresso.testing
	androidTestImplementation platform(libs.compose.bom)
	androidTestImplementation libs.ui.test.junit4

	debugImplementation libs.ui.tooling
	debugImplementation libs.ui.test.manifest
}